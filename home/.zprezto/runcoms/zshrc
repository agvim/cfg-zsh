#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

# Colors; no need to autoload zsh colors
# autoload -U colors && colors
if [ -f ~/.bash/display ]; then
    . ~/.bash/display
fi

# use variable expansion in prompt
setopt prompt_subst

# prompt using our defined colors
# <last command status> <vi_mode> <username>@<host> <cwd>
# $

# symbols for prompt
LEFT_SEP=''
LEFT_SEP_ALT=''
CHECKMARK='✔'
CROSS='✘'
PROMPT='%{$BASE02BG$BASE3%}%(?.%{$GREENBG%} $CHECKMARK%{$GREEN%}.%{$REDBG%} $CROSS%{$RED%}) ${vi_mode}%{$BASE02BG%} %{$CYAN%}%n%{$BASE0%}@%{$VIOLET%}%m%{$BASE0%} %{$BLUEBG$BASE02%}$LEFT_SEP %{$BASE3%}%~ %{$BLUE$BASE02BG%}$LEFT_SEP%E%{$RESET%}
%(!.%{$RED%}#.%{$ORANGE%}\$)%{$RESET%} '
# prepending a space since then the previous background color is preserved
vi_ins_mode="%{$YELLOWBG%}$LEFT_SEP%{$BASE3$BOLD%} INSERT %{$RESET$BASE02BG$YELLOW%}$LEFT_SEP"
vi_cmd_mode="%{$BASE1BG%}$LEFT_SEP%{$BASE3$BOLD%} NORMAL %{$RESET$BASE02BG$BASE1%}$LEFT_SEP"
# vim mode is cleaned up when issuing a command
# and a redraw is forced so it is only shown in the latest prompt
zle-line-finish() {
    vi_mode="%{$BASE02BG%}$LEFT_SEP"
    zle reset-prompt
}
# by default we are in insert mode
precmd() {
    vi_mode=$vi_ins_mode
}
# update status display when switching mode
zle-keymap-select() {
    vi_mode=${${KEYMAP/vicmd/${vi_cmd_mode}}/(main|viins)/${vi_ins_mode}}
    zle reset-prompt
}
# fix for when ctrl+c is issued
function TRAPINT() {
    vi_mode=""
    zle -R
    precmd
    return $(( 128 + $1 ))
}
zle -N zle-keymap-select
# timeout for mode change
export KEYTIMEOUT=1

# Alias definitions directory. Source all the files there
if [ -d ~/.sourced ]; then
    for F in ~/.sourced/*; do . $F; done
fi

# ls and path completion colors
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# history in tmp
export HISTFILE=/tmp/.zhistory$USER
